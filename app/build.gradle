plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-parcelize'
}

final VERSION_MAJOR = 1
final VERSION_MINOR = 1
final VERSION_PATCH = 4

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "io.cryptem.app"
        minSdkVersion 21
        targetSdkVersion 30
        //versionCode XYYYZZZ (X - Major, YYY - Minor, ZZZ - Patch)
        versionCode Integer.parseInt(String.valueOf(VERSION_MAJOR) + String.valueOf(VERSION_MINOR).padLeft(3, "0") + String.valueOf(VERSION_PATCH).padLeft(3, "0"))
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    Properties props = new Properties()
    props.load(new FileInputStream(new File("signing.properties")))
    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
        }
        release {
            storeFile file(props['keystore'])
            storePassword props['keystorePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        dev {
            dimension "env"
            applicationIdSuffix ".dev"
            buildConfigField 'String', 'API_URL', '"https://cryptem.io/"'
        }
        prod {
            dimension "env"
            buildConfigField 'String', 'API_URL', '"https://cryptem.io/"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

configurations {
    all {
        exclude group: 'xpp3', module: 'xpp3'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    implementation project(':arch')

    //Hilt
    implementation "com.google.dagger:hilt-android:2.38.1"
    kapt "com.google.dagger:hilt-android-compiler:2.38.1"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'

    implementation "androidx.room:room-runtime:2.3.0"
    implementation 'androidx.room:room-ktx:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'

    implementation platform('com.google.firebase:firebase-bom:26.4.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "androidx.fragment:fragment-ktx:1.3.6"

    //OkHttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.retrofit2:converter-moshi:2.8.1'
    implementation "com.squareup.retrofit2:converter-simplexml:2.9.0"

    implementation("com.squareup.moshi:moshi-kotlin:1.11.0")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.11.0")

    //Glide
    implementation "com.github.bumptech.glide:glide:4.11.0"
    kapt "com.github.bumptech.glide:compiler:4.11.0"

    //Google Play Services
    implementation 'com.google.gms:google-services:4.3.8'
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    //Google Maps
    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    //ZXing
    implementation 'com.google.zxing:core:3.4.0'
    implementation('com.journeyapps:zxing-android-embedded:4.2.0') { transitive = false }

    //MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:28.3.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}